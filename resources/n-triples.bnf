(* from https://www.w3.org/TR/n-triples/#n-triples-grammar *)

ntriplesDoc 	::= line*
line ::= WS* triple? EOL
triple 	::= 	subject WS* predicate WS* object WS* '.' WS*
subject 	::= 	IRIREF | BLANK_NODE_LABEL
predicate 	::= 	IRIREF
object 	::= 	IRIREF | BLANK_NODE_LABEL | literal
literal 	::= 	STRING_LITERAL_QUOTED ('^^' IRIREF | LANGTAG)?
LANGTAG 	::= 	'@' #"[a-zA-Z]"+ ('-' #"[a-zA-Z0-9]"+)*
EOL 	::= 	#"[\n\r]"+ 
WS 	::= 	#"[ \t]" | #"#.*"
IRIREF 	::= 	'<' IRI '>'
IRI ::= (#"[^\u0000-\u0020<>\"{}|^`\\]" | UCHAR)*
STRING_LITERAL_QUOTED 	::= 	'"' STRING_LITERAL  '"'
STRING_LITERAL ::= ( #"[^\u0022\u005C\u000A\u000D]" | ECHAR | UCHAR)*
BLANK_NODE_LABEL 	::= 	'_:' (PN_CHARS_U | #"[0-9]") ((PN_CHARS | '.')* PN_CHARS)?
UCHAR 	::= 	'\\u' HEX HEX HEX HEX | '\\U' HEX HEX HEX HEX HEX HEX HEX HEX
ECHAR ::= "\\" #"[tbnrf\"\'\\]"

HEX ::= #"[0-9A-Fa-f]"

PN_CHARS_BASE 	::= 	#"[A-Z]" | #"[a-z]" | #"[\u00C0-\u00D6]" |
                        #"[\u00D8-\u00F6]"  | #"[\u00F8-\u02FF]" | 
                        #"[\u0370-\u037D]"  | #"[\u037F-\u1FFF]" | 
                        #"[\u200C-\u200D]"  | #"[\u2070-\u218F]" |
                        #"[\u2C00-\u2FEF]"  | #"[\u3001-\uD7FF]" | 
                        #"[\uF900-\uFDCF]"  | #"[\uFDF0-\uFFFD]" | 
                        #"[\x{10000}-\x{EFFFF}]" 

PN_CHARS_U ::= PN_CHARS_BASE | ":" | "_" 

PN_CHARS 	::= 	PN_CHARS_U | "-" | #"[0-9]" | "\u00B7" | 
                        #"[\u0300-\u036F]" | #"[\u203F-\u2040]"


